---
title: "Stan program for finding informative prior InvGamma parameters"
format: html
---

Based on Section 3.2.3 of Robust Gaussian Process Modelling by Michael Betancourt.

<https://betanalpha.github.io/assets/case_studies/gaussian_processes.html#323_Informative_Prior_Model>

```{r}
library(posterior)
library(bayesplot)
library(rstan)
```

```{stan, output.var="model"}
functions {
  vector tail_delta(vector y, vector theta, array[] real x_r, array[] int x_i) {
    vector[2] deltas;
    deltas[1] = inv_gamma_cdf(theta[1] | exp(y[1]), exp(y[2])) - 0.01;
    deltas[2] = 1 - inv_gamma_cdf(theta[2] | exp(y[1]), exp(y[2])) - 0.01;
    return deltas;
  }
}

transformed data {
  real l = 2;
  real u = 20;
  vector[2] theta = [l, u]';

  real delta = 1;
  real a = square(delta * (u + l) / (u - l)) + 2;
  real b =  ((u + l) / 2) * ( square(delta * (u + l) / (u - l)) + 1);
  vector[2] y_guess = [log(a), log(b)]';

  array[0] real x_r;
  array[0] int x_i;

  vector[2] y = algebra_solver(tail_delta, y_guess, theta, x_r, x_i);

  print("a = ", exp(y[1]));
  print("b = ", exp(y[2]));
}
```

```{r}
rstan::sampling(model, iter=1, warmup=0, chains=1, seed=5838298, algorithm="Fixed_param")
```




